plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'maven-publish'
}

allprojects {
    // With this, we can reference java dependencies within git. https://www.jvt.me/posts/2021/10/27/gradle-plugin-jitpack/
    repositories {
        mavenCentral()
    }
    // Don't cache snapshots
    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

spotless {
    java {
        palantirJavaFormat()
    }
    groovyGradle {
        target '*.gradle' // default target of groovyGradle
        greclipse()
        indentWithSpaces()
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.guava:guava:32.1.1-jre'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'com.google.auto.service:auto-service:1.1.1'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

group = 'com.ziro.engineering'
version = resolveProjectVersion()

def resolveProjectVersion() {
    def gitDescribe = 'git describe --exact-match HEAD'.execute().text.trim()
    def version = file("${rootDir}/version.txt").text.trim()
    def branchName = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    return gitDescribe ? version : branchName + '-SNAPSHOT'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact("build/libs/espresso-${version}.jar") {
                extension 'jar'
            }
        }
    }

    repositories {
        maven {
            url = uri("https://repository.goziro.com/repository/engineering/")
            credentials {
                try {
                    username = System.getenv("SONATYPE_USERNAME") as String
                    password = System.getenv("SONATYPE_PASSWORD") as String
                } catch (Exception e) {
                    throw new Exception("SONATYPE_USERNAME and/or SONATYPE_PASSWORD environment variables are not set! Configure the following environment variables: SONATYPE_USERNAME=gradle SONATYPE_PASSWORD=<get password from 1pass>", e)
                }
            }
        }
    }
}

tasks.withType(PublishToMavenRepository).configureEach {
    def predicate = provider {
        version.contains('SNAPSHOT') || System.getenv().getOrDefault('CI_MODE', 'false') == 'true'
    }
    onlyIf("Artifact is a snapshot or running in CI") {
        predicate.get()
    }
}
